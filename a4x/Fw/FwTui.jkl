//
// Contains the user interface for the firmware.
//

#INCLUDE "<inc>/a4x.hjk"

PUBLIC FwMachineTypeString : ^UBYTE[XR_TYPE_MAX] = {
    [XR_STATION] = "XR/station",
    [XR_MP] = "XR/MP",
    [XR_FRAME] = "XR/frame",
}

PUBLIC FwSelectedBootPartition : ^FwDiskPartitionInfo

FN FwAutoSelectForBoot ()

    // If there's exactly one bootable device, we select that.
    // Otherwise, if there's a boot-dev specified in the NVRAM, we select that.
    // Failing both of those, we can't autoboot.

    RtlPrint ( "Autoboot: " )

    IF FwDeviceDatabase.BootableCount == 1 THEN
        // Find it.

        FwSelectedBootPartition = FwBootableTable[0]
    
    ELSEIF FwDeviceDatabase.BootableCount THEN
        // Look at NVRAM.

        diskname : UBYTE[16]

        IF FwGetNvramVariable (
            "boot-dev", // name
            &diskname[0], // buffer
            16, // bufsize
        ) THEN

            part := FwGetPartitionByName (
                &diskname[0], // buffer
            )

            IF part AND part^.BootInfo THEN
                FwSelectedBootPartition = part

            ELSE
                RtlPrint ( "'%s' is unavailable. ", &diskname[0] )
            END
        END
    END

    IF FwSelectedBootPartition THEN
        RtlPrint ( "dks%ds%d (%s)\n",
            FwSelectedBootPartition^.Id,
            FwSelectedBootPartition^.PartitionId,
            &FwSelectedBootPartition^.BootInfo^.OsName[0] )
    END
END

FN FwAutoBoot ()

    FwAutoSelectForBoot ()

    IF NOT FwSelectedBootPartition THEN
        RtlPrint ( "No selected boot device.\n" )
    
    ELSE
        FwBootPartition ( FwSelectedBootPartition )
    END
END

FN FwPrintBanner ()

    RtlPrint ( "\n    Welcome to %s!\n",
        FwMachineTypeString[FwDeviceDatabase.MachineType] )

    RtlPrint ( "    %dKB RAM.", FwDeviceDatabase.TotalRamBytes / 1024 )

    IF FwDeviceDatabase.MachineType != XR_STATION THEN
        RtlPrint ( " %d XR/17032 Processors.", FwDeviceDatabase.ProcessorCount )
    END

    RtlPrint ( "\n\n" )
END

EXTERN FN FwCommandMonitor ()

FN FwTextUserInterface ()

    // Print the banner.

    FwPrintBanner ()

    IF NOT FwSelectedBootPartition THEN
        // Depending on the value of the auto-boot? nvram variable, we want to
        // either go ahead and boot, or drop into the command monitor.

        varcontents : UBYTE[8]

        IF FwGetNvramVariableWithDefault (
            "auto-boot?", // name
            &varcontents[0], // buffer
            "true", // default
            8, // bufsize
        ) AND RtlCompareString ( &varcontents[0], "true" ) == 0 THEN

            FwAutoSelectForBoot ()
        END
    
    ELSE
        RtlPrint ( "Booting dks%ds%d...\n",
            FwSelectedBootPartition^.Id,
            FwSelectedBootPartition^.PartitionId )
    END

    IF NOT FwSelectedBootPartition THEN
        RtlPrint ( "No selected boot device.\n" )
    
    ELSE
        FwBootPartition ( FwSelectedBootPartition )
    END

    FwCommandMonitor ()
END